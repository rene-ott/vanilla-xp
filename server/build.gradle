plugins {
	id 'org.springframework.boot' version '2.4.2'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id "io.freefair.lombok" version "5.3.0"
	id 'java'
}

group = 'rscvanilla.xp'
version = '0.0.1'
sourceCompatibility = "11"
targetCompatibility = "11"

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web:2.4.2'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.4.2'
	implementation('org.postgresql:postgresql:42.2.18')
	implementation "org.jsoup:jsoup:1.13.1"
	implementation "commons-validator:commons-validator:1.7"
	implementation "com.google.guava:guava:30.1-jre"
	implementation "org.modelmapper:modelmapper:2.3.9"
	implementation "org.springframework.boot:spring-boot-starter-thymeleaf:2.4.2"
	developmentOnly "org.springframework.boot:spring-boot-devtools:2.4.2"

	testRuntimeOnly "com.h2database:h2:"
	testImplementation 'org.springframework.boot:spring-boot-starter-test:2.4.2'
}

task copyClientDistToResources(type: Copy) {
	dependsOn processResources
	dependsOn ":client:generateClientDist"

	from "${project(':client:').projectDir}/dist"
	into "${buildDir}/resources/main/static"
}

bootJar {
	dependsOn copyClientDistToResources
	archiveFileName = "server.${archiveExtension.get()}"

}

task copyConfig(type: Copy) {
	dependsOn bootJar
	from "${projectDir}/config"
	into "${buildDir}/libs/config"

	exclude "application-dev.yml"
}

task releaseApplication {
	dependsOn copyConfig
}


test {
	useJUnitPlatform()
}

